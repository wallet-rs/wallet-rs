plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.mozilla.rust-android-gradle.rust-android'
    id 'idea'
}

android {
    namespace 'android.WalletCore'
    compileSdk 33

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }

    defaultConfig {
        applicationId "android.WalletCore"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    ndkVersion '24.0.8215888'
}


android.applicationVariants.all { variant ->
    def uniffiPath = "${buildDir}/generated/source/uniffi/${variant.name}/java"
    def t = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
        workingDir "${project.projectDir}"
        // Runs the bindings generation, note that you must have uniffi-bindgen installed and in your PATH environment variable
        commandLine 'cargo', 'uniffi-bindgen', 'generate', "${workingDir}/../crates/core/src/WalletCore.udl", '--language', 'kotlin', '--out-dir', uniffiPath
    }
    variant.javaCompileProvider.get().dependsOn(t)
    def sourceSet = variant.sourceSets.find { it.name == variant.name }
    sourceSet.java.srcDir new File(uniffiPath)
    idea.module.generatedSourceDirs += file("${uniffiPath}/uniffi")
}
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
    module = "../crates/core"
    targetDirectory = "../target"
    libname = "libwallet_core"
    targets = ["arm", "arm64", "x86_64"]
    apiLevel = 24 //NDK API level, not SDK
    profile = 'release'
    extraCargoBuildArguments = ["--locked"]
}

dependencies {
    implementation "androidx.core:core-ktx:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation "net.java.dev.jna:jna:5.13.0@aar"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation "androidx.compose.runtime:runtime:1.0.0"
    implementation "androidx.compose.ui:ui:1.0.0"
    implementation "androidx.compose.foundation:foundation:1.0.0"
    implementation "androidx.compose.material:material:1.0.0"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}